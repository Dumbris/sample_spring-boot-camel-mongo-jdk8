buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
  }
}

plugins {
  id "java"
  id "jacoco"
  id "info.solidsoft.pitest" version "1.1.1"
  id "com.ewerk.gradle.plugins.integration-test" version "1.0.6"
  id "com.ewerk.gradle.plugins.auto-value" version "1.0.1"
}

apply plugin: "spring-boot"

dependencies {
  // Spring boot
  compile "org.springframework.boot:spring-boot-actuator:$springBootVersion"
  compile "org.springframework.boot:spring-boot-starter-data-mongodb:$springBootVersion"
  compile "org.springframework.boot:spring-boot-starter-logging:$springBootVersion"
  compile "org.springframework.boot:spring-boot-starter-jetty:$springBootVersion"
  compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion") {
    exclude module: "spring-boot-starter-tomcat"
  }

  // Jetty9
  compile "org.eclipse.jetty:jetty-webapp:$jettyVersion"

  // Camel
  compile("org.apache.camel:camel-spring-javaconfig:$camelVersion") {
    exclude group: "org.springframework", module: "spring-test"
  }
  compile "org.apache.camel:camel-quartz2:$camelVersion"

  // Utils
  compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
  compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
  compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
  compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
  compile "com.google.guava:guava:$guavaVersion"
  compile "com.google.auto.value:auto-value:$autoValueVersion"
  compile "edu.washington.cs.types.checker:checker-framework:$checkerFrameworkVersion"

  // Testing
  testCompile "org.testng:testng:$testngVersion"
  testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
  testCompile "com.jayway.restassured:rest-assured:$restAssuredVersion"
  testCompile "org.assertj:assertj-core:$assertjVersion"
}

test {
  useTestNG()

  minHeapSize = "128m"
  maxHeapSize = "512m"
}

integrationTest {
  useTestNG()

  minHeapSize = "128m"
  maxHeapSize = "512m"
}

jar {
  manifest {
    attributes(
        "Implementation-Title": "com.ewerk.prototype.Application",
        "Implementation-Version": version,
        "Built-JDK": System.getProperty("java.version"),
        "Built-Gradle": gradle.gradleVersion
    )
  }
}